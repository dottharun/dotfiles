export XDG_CONFIG_HOME=$HOME/.config

PERSONAL=$XDG_CONFIG_HOME/personal

# personal scripts
source $PERSONAL/env
source $PERSONAL/path
source $PERSONAL/alias
source $PERSONAL/alias-git
source $PERSONAL/.env

export LC_ALL=C.UTF-8
export LANG=C.UTF-8
export GOPATH=$HOME/go
export JAVA_HOME=/opt/java/jdk-23.0.1
export NIXPKGS_ALLOW_UNFREE=1
export EDITOR='nvim'
# for heap allocation errs in node - default is 2gb
export NODE_OPTIONS="--max-old-space-size=8192"

# to control compilation of cpp versions --not sure if it really does anything
export CC="clang-18"
export CXX="clang++-18"

# # gaussian software
# # export g16root=/home/tharun
# # export GAUSS_SCRDIR=/home/tharun/g16/Scratch
# # source /home/tharun/g16/bsd/g16.profile
# export g16root=$HOME/opt/gaussian
# export GAUSS_EXEDIR=$g16root/g16
# export GAUSS_SCRDIR=$g16root/scr
# source $g16root/g16/bsd/g16.profile
# export PGI_FASTMATH_CPU=haswell
# addToPath $GAUSS_EXEDIR
#
# # gaussview software
# export GV_DIR=/home/tharun/gv/gview
# export PATH="$PATH:/home/tharun/gv/gview"
# alias -g gv=/home/tharun/gv/gview.sh

##############
# BASIC SETUP
##############

typeset -U PATH
autoload colors; colors;

##########
# HISTORY
##########

HISTFILE=$HOME/.zsh_history
HISTSIZE=50000
SAVEHIST=50000

setopt INC_APPEND_HISTORY     # Immediately append to history file.
setopt EXTENDED_HISTORY       # Record timestamp in history.
setopt HIST_EXPIRE_DUPS_FIRST # Expire duplicate entries first when trimming history.
setopt HIST_IGNORE_DUPS       # Dont record an entry that was just recorded again.
setopt HIST_IGNORE_ALL_DUPS   # Delete old recorded entry if new entry is a duplicate.
setopt HIST_FIND_NO_DUPS      # Do not display a line previously found.
setopt HIST_IGNORE_SPACE      # Dont record an entry starting with a space.
setopt HIST_SAVE_NO_DUPS      # Dont write duplicate entries in the history file.
setopt SHARE_HISTORY          # Share history between all sessions.
unsetopt HIST_VERIFY          # Execute commands using history (e.g.: using !$) immediately
setopt PRINT_EXIT_VALUE       # To print exit codes if not zero

# still cant find the use it was causing problems when bash globbing is expected
# setopt NO_GLOB

#############
# COMPLETION
#############

# Speed up completion init, see: https://gist.github.com/ctechols/ca1035271ad134841284
autoload -Uz compinit
for dump in ~/.zcompdump(N.mh+24); do
    compinit
done
compinit -C

# unsetopt menucomplete
unsetopt flowcontrol
setopt auto_menu
setopt complete_in_word
setopt always_to_end
setopt auto_pushd

zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'

###############
# KEY BINDINGS
###############

# Vim Keybindings
bindkey -v

# The time the shell waits, in hundredths of seconds, for another key
# to be pressed when reading bound multi-character sequences.
#
# Set to shortest possible delay is 1/100 second.
# This allows escape sequences like cursor/arrow keys to work,
# while eliminating the delay exiting vi insert mode.
#
# from here https://www.reddit.com/r/vim/comments/60jl7h/zsh_vimode_no_delay_entering_normal_mode/
# TODO: Might break some keys
KEYTIMEOUT=1

# Open line in Vim by pressing 'v' in Command-Mode
autoload -U edit-command-line
zle -N edit-command-line
bindkey -M vicmd v edit-command-line

# Push current line to buffer stack, return to PS1
bindkey "^Q" push-input

# Make up/down arrow put the cursor at the end of the line
# instead of using the vi-mode mappings for these keys
bindkey "\eOA" up-line-or-history
bindkey "\eOB" down-line-or-history
bindkey "\eOC" forward-char
bindkey "\eOD" backward-char

# CTRL-R to search through history
bindkey '^R' history-incremental-search-backward
# CTRL-S to search forward in history
bindkey '^S' history-incremental-search-forward
# Accept the presented search result
bindkey '^Y' accept-search

# Use the arrow keys to search forward/backward through the history,
# using the first word of what's typed in as search word
bindkey '^[[A' history-search-backward
bindkey '^[[B' history-search-forward

# Use the same keys as bash for history forward/backward: Ctrl+N/Ctrl+P
bindkey '^P' history-search-backward
bindkey '^N' history-search-forward

# Backspace working the way it should
bindkey '^?' backward-delete-char
bindkey '^[[3~' delete-char

# Where should I put you?
bindkey -s ^f "tmux-sessionizer\n"

########
# ENV
########

# Reduce delay for key combinations in order to change to vi mode faster
# See: http://www.johnhawthorn.com/2012/09/vi-escape-delays/
# Set it to 10ms
export KEYTIMEOUT=1

# Set up fzf key bindings and fuzzy completion
source <(fzf --zsh)

#########
# PROMPT
#########

## both funcs -  to show time diff
function preexec() {
  timer=$(date +%s%3N)
}

function precmd() {
  if [ $timer ]; then
    local now=$(date +%s%3N)
    local d_ms=$(($now-$timer))
    local d_s=$((d_ms / 1000))
    local ms=$((d_ms % 1000))
    local s=$((d_s % 60))
    local m=$(((d_s / 60) % 60))
    local h=$((d_s / 3600))
    if ((h > 0)); then elapsed=${h}h${m}m
    elif ((m > 0)); then elapsed=${m}m${s}s
    elif ((s >= 10)); then elapsed=${s}.$((ms / 100))s
    elif ((s > 0)); then elapsed=${s}.$((ms / 10))s
    else elapsed=${ms}ms
    fi

    export RPROMPT="%F{cyan}${elapsed} %{$reset_color%}"
    unset timer
  fi
}

setopt prompt_subst

git_prompt_info() {
  local dirstatus=" OK"
  local dirty="%{$fg_bold[red]%} X%{$reset_color%}"

  # does'nt care about untracked files
  if [[ ! -z $(git status --porcelain 2> /dev/null | tail -n1) ]]; then
    dirstatus=$dirty
  fi

  ref=$(git symbolic-ref HEAD 2> /dev/null) || \
  ref=$(git rev-parse --short HEAD 2> /dev/null) || return
  echo " %{$fg_bold[blue]%}${ref#refs/heads/}$dirstatus%{$reset_color%}"
}

local dir_info_color="%B"

local dir_info="%{$dir_info_color%}%(5~|%-1~/.../%2~|%4~)%{$reset_color%}"

local arrow="%{$fg_bold[green]%}$ %{$reset_color%}"

# local user="%{$fg[cyan]%}%n %{$reset_color%}"

NEWLINE=$'\n'
PROMPT='${NEWLINE}${dir_info}$(git_prompt_info)${NEWLINE}${arrow}'

# dont know how but this eval is still fast
eval "$(zoxide init --cmd cd zsh)"

# meshery
# source <(mesheryctl completion zsh)

# emscripten
# source ~/thirdparty/emsdk/emsdk_env.sh

#direnv
eval "$(direnv hook zsh)"

# meshery
# source <(mesheryctl completion zsh)
